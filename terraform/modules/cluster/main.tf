resource "scaleway_vpc_private_network" "my_network" {
  name = "voteapp-private-network"
  tags = ["vote-app"]
}

resource "scaleway_k8s_cluster" "vote" {
  name                        = var.cluster_name
  region                      = var.region
  version                     = "1.32.3"
  cni                         = "cilium"
  delete_additional_resources = true
  private_network_id          = scaleway_vpc_private_network.my_network.id

  tags = ["vote-app"]
}

resource "scaleway_secret" "kubeconfig" {
  name        = "kubeconfig"
  path        = "/vote-app/cluster/"
  protected   = false
  project_id  = var.project_id
  description = "generated by terraform"
}

resource "scaleway_secret_version" "kubeconfig" {
  data        = base64encode(scaleway_k8s_cluster.vote.kubeconfig[0].config_file)
  secret_id   = scaleway_secret.kubeconfig.id
  description = "generated by terraform"
}

resource "scaleway_k8s_pool" "pool" {
  cluster_id  = scaleway_k8s_cluster.vote.id
  name        = "default-pool"
  node_type   = "DEV1-M"
  size        = 1
  autoscaling = true
  min_size    = 1
  max_size    = 3
  zone        = var.zone
  tags        = ["vote-app"]
}
